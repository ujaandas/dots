{
  description = "Example nix-darwin system flake";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";

    darwin = {
      url = "github:nix-darwin/nix-darwin/master";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    nix-homebrew = {
      url = "github:zhaofengli-wip/nix-homebrew";
    };

    homebrew-bundle = {
      url = "github:homebrew/homebrew-bundle";
      flake = false;
    };

    homebrew-core = {
      url = "github:homebrew/homebrew-core";
      flake = false;
    };

    homebrew-cask = {
      url = "github:homebrew/homebrew-cask";
      flake = false;
    };

    nix-vscode-extensions.url = "github:nix-community/nix-vscode-extensions";

    agenix.url = "github:ryantm/agenix";
  };

  outputs =
    inputs@{
      self,
      nixpkgs,
      darwin,
      home-manager,
      nix-homebrew,
      homebrew-bundle,
      homebrew-core,
      homebrew-cask,
      nix-vscode-extensions,
      agenix,
    }:
    let
      username = "ooj";
      # todo: either use flake-utils or make helper funcs
      system = "aarch64-darwin";
    in
    {
      darwinConfigurations.${username} = darwin.lib.darwinSystem {
        specialArgs = inputs // {
          inherit username;
        };
        modules = [
          ./hosts/darwin/default.nix
          agenix.nixosModules.default
          home-manager.darwinModules.home-manager
          nix-homebrew.darwinModules.nix-homebrew
        ];
      };

      devShells.${system}.default = nixpkgs.legacyPackages.${system}.mkShell {
        buildInputs = [
          self.packages.${system}.search
          self.packages.${system}.build
          self.packages.${system}.activate
          self.packages.${system}.rebuild
          self.packages.${system}.format
          self.packages.${system}.lint
          self.packages.${system}.test-all
          self.packages.${system}.check
        ];
      };

      packages.${system} =
        let
          pkgs = nixpkgs.legacyPackages.${system};

          mkScript =
            name: text:
            pkgs.writeShellApplication {
              inherit name;
              inherit text;
              runtimeInputs = with pkgs; [
                nixfmt-tree
                statix
              ];
            };
        in
        {
          search = mkScript "search" ''
            echo "üîç Starting nixpkgs REPL..."
            nix repl -f '<nixpkgs>'
            echo "üëã REPL exited."
          '';

          build = mkScript "build" ''
            echo "üî® Building system flake..."
            if sudo darwin-rebuild build --flake .#${username}; then
              echo "‚úÖ Build completed successfully."
            else
              echo "‚ùå Build failed."
              exit 1
            fi
          '';

          activate = mkScript "activate" ''
            echo "üöÄ Activating system..."
            if sudo result/activate; then
              echo "‚úÖ Activation completed successfully."
            else
              echo "‚ùå Activation failed."
              exit 1
            fi
          '';

          format = mkScript "format" ''
            echo "üé® Formatting Nix code..."
            if treefmt --walk git; then
              echo "‚úÖ Formatting completed successfully."
            else
              echo "‚ùå Formatting failed."
              exit 1
            fi
          '';

          lint = mkScript "lint" ''
            echo "üîç Linting Nix code..."
            if statix check --ignore result .direnv; then
              echo "‚úÖ Linting passed with no issues."
            else
              echo "‚ùå Linting failed: issues detected."
              exit 1
            fi
          '';

          check = mkScript "check" ''
            echo "‚òëÔ∏è Checking flake..."
            if nix flake check; then
              echo "‚úÖ Flake check passed with no issues."
            else
              echo "‚ùå Flake check failed: issues detected."
              exit 1
            fi
          '';

          test-all = mkScript "test-all" ''
            echo "üß™ Testing system..."
            check || exit 1
            format || exit 1
            lint || exit 1
            build || exit 1
            echo "‚úÖ Test completed successfully."
          '';

          rebuild = mkScript "rebuild" ''
            echo "üîÅ Rebuilding system..."
            test-all || exit 1
            activate || exit 1
            echo "‚úÖ Rebuild completed successfully."
          '';
        };
    };
}
